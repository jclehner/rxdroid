apply plugin: 'com.android.application'

def askForKeystorePassword() {
	def prompt =
			"\n==========================================================" +
			"\nKeystore : ${android.signingConfigs.release.storeFile}"     +
			"\nKey alias: ${android.signingConfigs.release.keyAlias}"      +
			"\n==========================================================" +
			"\nPassword: "

	def console = System.console()

	if (console) {
		return new String(console.readPassword(prompt))
	} else {
		return ""
	}
}

buildscript {
	repositories {
		mavenCentral()
		maven {
			url 'https://maven.google.com/'
			name 'Google'
		}
		google()
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:3.5.3'
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://maven.google.com/'
		name 'Google'
	}
	jcenter()
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
		implementation 'com.j256.ormlite:ormlite-core:4.48'
		implementation 'com.j256.ormlite:ormlite-android:4.48'
		implementation 'androidx.legacy:legacy-support-v4:1.0.0'
		implementation 'androidx.legacy:legacy-support-v13:1.0.0'
		implementation 'androidx.appcompat:appcompat:1.1.0'
		implementation 'net.lingala.zip4j:zip4j:1.3.3'
		implementation 'joda-time:joda-time:2.10.5'
		implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.0'
}


android {
	buildToolsVersion '28.0.3'
	compileSdkVersion 29

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 29

        applicationId 'at.jclehner.rxdroid';

		versionCode 9329
		versionName '0.9.33'
	}

	lintOptions {
		warning 'MissingTranslation'
	}

	signingConfigs {
		release {
			def keystore = System.getenv("RXDROID_KEYSTORE")
			def console = System.console()
			if (keystore && console) {
				storeFile file(keystore)
				keyAlias System.getenv("RXDROID_KEY_ALIAS")
				storePassword askForKeystorePassword()
				keyPassword storePassword
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFile 'proguard-project.txt'
			signingConfig signingConfigs.release
		}
		debug {
			applicationIdSuffix '.debug'
		}
	}
}
